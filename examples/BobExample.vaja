import Bob

fn requireAuth(username, password)
  fn (conn)
    if (verifyBasicAuth(username, password, conn))
      []
    else
      [{"respStatus": 401, "respBody": "Denied"}]
    end
  end
end

fn verifyBasicAuth(username, password, conn)
  let authHeaders = Array.filter(fn(x) -> x[0] == "authorization", conn.headers)
  if (Array.len(authHeaders) == 0)
    return false
  end

  let [_, headerValue] = authHeaders |> Array.head()
  let encodedValue = headerValue |> String.split(" ") |> Array.last()
  let [reqUsername, reqPassword] = \
    Base64.decode(encodedValue) \
    |> String.split(":")
  (username == reqUsername) and (password == reqPassword)
end

fn handlerDecorators(guards, handler)
  fn (conn, args)
    let res = Array.reduce(fn(acc, curr) -> acc ++ curr(conn), [], guards)
    if (Array.len(res) == 0)
      handler(conn, args)
    else
      res |> Array.head()
    end
  end
end

let app = \
  Bob.new() \
  |> Bob.addMiddlewares([
    Bob.enforceTrailingSlashMiddleware,
    Bob.debugMiddleware,
    Bob.rendererMiddleware
  ]) \
  |> Bob.addRoute404(fn(conn, _) -> {"respStatus": 404, "respBody": "Page not found"}) \
  |> Bob.addRoutes([
    [
      Bob.newPath("/artist/([a-z]*)/"),
      fn(conn, args)
        Bob.newJSONResponse({"respStatus": 200, "context": {
          "artist": args[0]
        }})
      end |> handlerDecorators([requireAuth("admin", "password")])
    ],
    [
      Bob.newPath("/albums/([a-z]*)/"),
      fn(conn, args)
        Bob.newJSONResponse({"respStatus": 200, "context": {
          "album": args[0]
        }})
      end
    ]
  ])

Http.createServer() \
|> Http.addHandler(
  app |> Bob.makeHandler()
) \
|> Http.listen(8080)
